buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.0.0"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
    }
}



apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'asset-pipeline'

group = 'com.morpheusdata.phpipam'
sourceCompatibility = '1.11'
targetCompatibility = '1.11'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	provided
}

dependencies {
	provided "com.morpheusdata:morpheus-plugin-api:$plugincore"
	provided "org.codehaus.groovy:groovy-all:$groovyVersion"
	implementation 'commons-beanutils:commons-beanutils:1.9.3'
	implementation "org.slf4j:slf4j-api:$slf4jVersion"
	implementation "org.slf4j:slf4j-parent:$slf4jVersion"
	implementation 'commons-net:commons-net:3.6'
	implementation 'commons-validator:commons-validator:1.7'

	// Include morpheus-core and it's dependencies
	testImplementation 'io.reactivex.rxjava3:rxjava:3.1.8'
	testImplementation 'org.apache.httpcomponents:httpclient:4.5.3'
	testImplementation 'org.apache.httpcomponents:httpcore:4.4.5'
	testImplementation "org.slf4j:slf4j-parent:$slf4jVersion"

	testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
	testImplementation 'net.bytebuddy:byte-buddy:1.9.3'
	testImplementation 'org.objenesis:objenesis:2.6'
	testImplementation "org.spockframework:spock-core:$spockVersion"
	testImplementation 'cglib:cglib-nodep:3.2.12'
}


sourceSets {
	main {
		compileClasspath += configurations.provided
	}
}

jar {
	manifest {
		attributes(
				'Plugin-Class': 'com.morpheusdata.phpipam.PhpIpamPlugin',
				'Plugin-Version': version,
				'Morpheus-Name': '{php}IPAM',
				'Morpheus-Organization': 'morpheus',
				'Morpheus-Code': 'phpipam-plugin',
				'Morpheus-Description': 'Plugin for {php}IPAM',
				'Morpheus-Logo': 'assets/phpipam-black.svg',
				'Morpheus-Logo-Dark': 'assets/phpipam-white.svg',
				'Morpheus-Color': '#ffffff',
				'Morpheus-Labels': 'plugin,networking,ipam',
				'Morpheus-Repo': 'https://github.com/gomorpheus/morpheus-phpipam-plugin',
				'Morpheus-Min-Appliance-Version': minversion
		)
	}
}


task(console, dependsOn: 'classes', type: JavaExec) {
	main = 'groovy.ui.Console'
	classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

shadowJar {
	// archiveFileName = "${baseName}-${version}.mpl"
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}


tasks.assemble.dependsOn tasks.shadowJar
